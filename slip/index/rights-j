#!/l/local/bin/perl

=head NAME

rights-j

=head USAGE

% rights-j [-T][-B][-I][-d{1|2}]

=head DESCRIPTION

=head1 DATA SCHEMA

The VuFind Solr index document has two fields to support mdp.j_rights
table updates to HathiTrust IDs (nid's: n]amespace + id).

=over 4

=item ht_id_display [stored]:

a repeating field that contains the nid(s) of volumes associated with
the bib record together with the date each item was last updated and
an optional enumcron if the work is multi-volume. For example:

<arr name="ht_id_display">
  <str>mdp.39015066198014|20090701|v.1</str>
  <str>mdp.39015025339659|00000000|v.10</str>
  <str>mdp.39015066198311|00000000|v.2</str>
</arr>

=item ht_id_update [not stored]:

the date when the bib record was last updated due to a change/addition
to the list in the ht_id_display field. One or more dates on the items
in the ht_id_didplay field will be the same as this field's value. In
the above example, ht_id_update would be 20090701.

=back

=head1 QUERYING

rights-j does a query based on timestamp=D, where D is 2 days earlier
than the last run of rights-j.  The lag is to avoid overlooking
records than could have been added to the VuFind Solr index after
j-rights ran that day

=over 4

=item q=ht_id_update:[D TO *],fl=ht_id_display

These are bib records for added or updated (rights changed) volumes
but not for volumes where just the record-level metadata has changed.
These volumes correspond to ht_id_display items with dates equal or
newer that D. Just these volumes re-indexed to reflect their addition,
rights changes or reloaded status.

=back

=head1 PROCESSING

rights-j gets timestamp=D from the j_vsolr_timestamp table which
records the newest update_time seen when rights-j last ran.

rights-j parses the item ids from the bib records and queries
mdp.rights table for the complete set of rights data for that id and
REPLACES INTO the mdp.j-rights with an update_time=vsolr_update_time.

enqueuer-j script processes records with an update_time newer than the
newest update_time seen on any record the last time it ran. Processing
means to copy from mdp.j-rights into mdp.j-queue to be later consumed
by the index-j script.

The -R option drops the entire j_rights_temp table, rebuilds it and
renames it to j_rights.

=head1 OPTIONS

=over 8

=item see help

see help

=back

=cut

use strict;
BEGIN {
    #$ENV{DEBUG_LOCAL} = 1;
}

# ----------------------------------------------------------------------
# Set up paths for local libraries -- must come first
# ----------------------------------------------------------------------
use lib "$ENV{SDRROOT}/mdp-lib/Utils";
use Vendors;


# Perl
use Getopt::Std;
use Time::HiRes;

# App
use Utils;
use Utils::Time;
use Utils::GlobalSwitch;
use Debug::DUtils;

use Context;
use MdpConfig;
use Database;
use Identifier;
use MetadataQueue;

use Search::Site;
use Search::Constants;
use Search::Result::vSolrRaw;


# Local
use Db;
use vSolr;
use SLIP_Utils::Common;
use SLIP_Utils::States;
use SLIP_Utils::Log;
use SLIP_Utils::Load;
use SLIP_Utils::Solr;

my $INTERACTIVE = $ENV{TERM};

if ($INTERACTIVE) {
    if (Utils::GlobalSwitch::cron_jobs_disabled('slip', 'STOPSLIP')) {
        __output("Cannot get rights. STOPSLIP in place!!\n");
        exit 0;
    }
}
else {
    Utils::GlobalSwitch::Exit_If_cron_jobs_disabled('STOPSLIP');
}


# Prevent replication errors: enqueue only to one mysqlserv
if (Search::Site::get_server_site_name() ne 'macc') {
    __output("Must be run only from site=MACC\n");
    exit 0;
}

sub r_get_usage {
    my $s;
    $s .= qq{Usage: rights-j [-n][-f][-T][-B -r run | -R | -F<file>] [-d{1|2|3|4|5|6}]\n};
    $s .= qq{     -f force email\n};
    $s .= qq{     -T test compilation only. No operations\n};
    $s .= qq{     -F insert IDs from file (missing from j_rights)\n};
    $s .= qq{           where -n is check_only, no inserts to j_rights\n};
    $s .= qq{     -B build from last vSolr timestamp for -r run\n};
    $s .= qq{           where -n is check_only, no inserts to j_rights\n};
    $s .= qq{     -R re-build j_rights_temp from 0000000\n};
    $s .= qq{           where -n is check_only, NO DROP of j_rights\n};
    $s .= qq{     -d options\n};
    $s .= qq{           1: me\n};
    $s .= qq{           2: me,lsdb\n};
    $s .= qq{           3: me,vsolr\n};
    $s .= qq{           4: me,vsolrlibxml\n};
    $s .= qq{           5: me,query\n};
    $s .= qq{           6: me,query,response\n};
    $s .= qq{           7: me,lsdb,repair\n};
    return $s;
}

our ($opt_B, $opt_R, $opt_d, $opt_n, $opt_T, $opt_F, $opt_r, $opt_f);

my $ops = getopts('BRd:nTI:r:fF:');
my $FORCE_EMAIL = defined($opt_f);

if ($opt_d) {
    if ($opt_d == 1) {
        $ENV{'DEBUG'} .= 'me';
    }
    elsif ($opt_d == 2) {
        $ENV{'DEBUG'} .= 'me,lsdb';
    }
    elsif ($opt_d == 3) {
        $ENV{'DEBUG'} .= 'me,vsolr';
    }
    elsif ($opt_d == 4) {
        $ENV{'DEBUG'} .= 'me,vsolrlibxml';
    }
    elsif ($opt_d == 5) {
        $ENV{'DEBUG'} .= 'me,query';
    }
    elsif ($opt_d == 6) {
        $ENV{'DEBUG'} .= 'me,query,response';
    }
    elsif ($opt_d == 7) {
        $ENV{'DEBUG'} .= 'me,lsdb,repair';
    }
}

use constant STOPSLIP_SLEEP_WAIT => 60;

my $BUILD = 0;
my $RUN = $opt_r;
my $REBUILD = 0;
my $CHECK = 0;
my $ID_INSERT_FILE;

if (! defined($RUN)) {
    my $s = r_get_usage();
    __output(qq{Missing run (-r) argument\n} . $s);
    exit $SLIP_Utils::States::RC_BAD_ARGS;
}

if (defined($opt_n)) {
    $CHECK = 1;
}

if (defined($opt_F)){
    $ID_INSERT_FILE = $opt_F;
}
elsif (defined($opt_B)) {
    $BUILD = 1;
}
elsif (defined($opt_R)) {
    $REBUILD = 1;
}
else {
    print r_get_usage();
    exit 0;
}

my $TEST_ONLY = defined($opt_T);
exit 0 if ($TEST_ONLY);

# log to different files for incremental build vs. full rebuild
my $RIGHTS_LOGFILE_KEY = ($REBUILD ? 'rights_rebuild_logfile' : 'rights_logfile');

# Do not try to build shadow rights table while I am running already
my $Lock_File = "/tmp/rights-lock.sem";
my $Semaphore = new Semaphore($Lock_File);
if (! $Semaphore) {
    my $rc = $SLIP_Utils::States::RC_RIGHTS_NO_SEM;
    my $s = qq{rights-j could not get semaphore\n};
    __output($s);
    __non_interactive_err_output($rc, $s);

    exit $rc;
}
END {
    $Semaphore->unlock()
        if ($Semaphore);
}

# Flush i/o
$| = 1;

my $SLICE_SIZE = ($REBUILD ? 100 : 1000);

my $C = new Context;

# Any run number will do to grab common.conf
my $config = SLIP_Utils::Common::gen_run_config('slip', $RUN);
$C->set_object('MdpConfig', $config);

my $db;
eval {
    $db = new Database($config);
};
if ($@) {
    SLIP_Utils::Common::Log_database_connection_error($C, 'rights-j', $@);
    exit $SLIP_Utils::States::RC_DATABASE_CONNECT;
}

$C->set_object('Database', $db);
my $DBH = $C->get_object('Database')->get_DBH();

my $INIT_MSG;

eval {
    if (r_rights_enabled($C, $DBH) || $INTERACTIVE || $REBUILD) {
        my $now_timestamp = SLIP_Utils::Common::get_now_ISO_timestamp();

        if ($ID_INSERT_FILE) {
            my $i = $CHECK ? 'CHECK (NO insert)' : 'INSERT';

            __confirm("\nProcess j_rights: $i IDs from $ID_INSERT_FILE? [N] ");
            repair_insert_id_helper($C, $DBH, $ID_INSERT_FILE, $CHECK);
        }
        elsif ($BUILD) {
            # Build from last timestamp
            my $last_timestamp = Db::Select_vSolr_timestamp($C, $DBH);
            my $lag = $C->get_object('MdpConfig')->get('vSolr_num_lag_days');
            my $query_timestamp = SLIP_Utils::Common::get_offset_ISO_timestamp($last_timestamp, $lag);

            $INIT_MSG = qq{Today is $now_timestamp.\n}
                . qq{Last run set j_rights MAX(update_time) to $last_timestamp. Query will be [$query_timestamp TO *]};

            if ($CHECK) {
                __confirm("\n$INIT_MSG\nCheck j_rights table: NO j_rights INSERT? [N] ");
            }
            else {
                __confirm("\n$INIT_MSG\nExtend j_rights table: INSERT INTO j_rights? [N] ");
            }

            # Go
            my ($process_start, $totals_hashref) = process_rights($C, $DBH, $RUN, $last_timestamp, $CHECK, 0);

	    r_final_report($C, $DBH, $process_start, $totals_hashref, $CHECK, $REBUILD);
        }
        elsif ($REBUILD) {
            # Build from 00000000
            $INIT_MSG = qq{Today is $now_timestamp. Rebuild from '00000000'. }
                . qq{Query will be ['00000000' TO *]};

            if ($CHECK) {
                __confirm("\n$INIT_MSG\nCHECK=1 CREATE j_rights_temp\n  => NO RENAME j_rights_temp, NO DROP j_rights? [N] ");
            }
            else {
                __confirm("\n$INIT_MSG\nCHECK=0 RENAME j_rights_temp to j_rights\n  => DROP j_rights? [N] ");
            }

            # DROP/CREATE j_rights_temp
            initialize_j_rights_temp($C, $DBH);
            # Always insert into j_rights_temp by forcing CHECK=0 here
	    my ($process_start, $totals_hashref) = process_rights($C, $DBH, undef, '00000000', 0, 1);

	    r_final_report($C, $DBH, $process_start, $totals_hashref, $CHECK, $REBUILD);

            # DO NOT DROP j_rights if CHECK==1. j_rights_temp is left
            # behind until next (-R) run.
            if (! $CHECK) {
                DROP_j_rights_RENAME_j_rights_temp($C, $DBH);
            }
        }

        # Flush remaining logs to disk
        flush_r_rights_Log($C, $RIGHTS_LOGFILE_KEY);
    }
    else {
        __output(qq{j_rights not enabled\n})
            unless $INTERACTIVE;
    }
};
if ($@) {
    my $rc = $SLIP_Utils::States::RC_CRITICAL_ERROR;

    my $s = qq{CRITICAL ERROR: $@\n};
    __output($s);
    __non_interactive_err_output($rc, $s);

    exit $rc;
}

exit 0;

#
# --------------------- S u b r o t i n e s   -------------------------
#

# ---------------------------------------------------------------------

=item repair_insert_id_helper

Description

=cut

# ---------------------------------------------------------------------
sub repair_insert_id_helper {
    my ($C, $dbh, $filename, $Check) = @_;

    my $id_arr_ref = SLIP_Utils::Load::load_ids_from_file($C, $filename);
    __output(scalar(@$id_arr_ref) . qq{ ids loaded from $filename\n});

    my $searcher = SLIP_Utils::Solr::create_VuFind_Solr_Searcher_by_alias($C);
    my $rs = new Search::Result::vSolrRaw();

    foreach my $id (@$id_arr_ref) {
        repair_insert_id($C, $dbh, $id, $Check, $searcher, $rs);
    }
}

# ---------------------------------------------------------------------

=item repair_insert_id

Description

=cut

# ---------------------------------------------------------------------
sub repair_insert_id {
    my ($C, $dbh, $nid, $Check, $searcher, $rs) = @_;

    my $s;

    $s = "Repair: start process $nid";
    __output("$s\n");
    r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

    my $safe_nid = Identifier::get_safe_Solr_id($nid);
    my $query = qq{q=ht_id:$safe_nid&start=0&rows=1&fl=id,ht_id_display};
    $rs = $searcher->get_Solr_raw_internal_query_result($C, $query, $rs);

    my $ref_to_vSolr_response;

    if ($rs->http_status_ok()) {
        if ($rs->get_num_found() > 0) {
            $ref_to_vSolr_response = $rs->get_complete_result();
            DEBUG('vufind', qq{VuFind: response="$$ref_to_vSolr_response"});
        }
        else {
            DEBUG('vufind', qq{VuFind: response="EMPTY" code=} . $rs->get_response_code());
            my $status = $rs->get_status_line();
            $s = qq{Repair: Solr $nid not found in VuFind, status=$status, fail query="$query"};
            __output("$s\n");
            r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

            return 'VuFind_MISSING';
        }
    }
    else {
        DEBUG('vufind', qq{VuFind: bad HTTP response code=} . $rs->get_response_code());
        my $status = $rs->get_status_line();
        $s = "Repair: Solr bad VuFind HTTP response ($query): $status";
        __output("$s\n");
        r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

        return 'VuFind_BADHTTP';
    }

    my ($namespace, $barcode) = split(/\./, $nid);
    my $rights_hashref = Db::Select_latest_rights_row($C, $dbh, $namespace, $barcode);
    if (! $rights_hashref) {
        $s = "Repair: Rights lookup $nid fail";
        DEBUG('vufind', $s);
        __output("$s\n");
        r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

        return 'Rights_FAIL';
    }

    my $insert_hashref = $rights_hashref;

    my ($ht_update_time) = ($$ref_to_vSolr_response =~ m,<arr name="ht_id_display">.*?<str>\Q$nid\E\|(.*?)\|.*?</str>,s);
    my ($sysid) = ($$ref_to_vSolr_response =~ m,<str name="id">(.*?)</str>,s);

    $insert_hashref->{'sysid'} = $sysid;
    $insert_hashref->{'timestamp_of_nid'} = $ht_update_time;

    my $case = Db::Replace_j_rights_id($C, $dbh, $insert_hashref, $Check, 0);
    if (! grep(/^$case$/, ('NEW', 'NOOP', 'MOVED', 'UPDATED'))) {
        $s = qq{Repair: Software error: invalid case value=$case } . Utils::Time::iso_Time();
        r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

        return 'rights-j_SOFTWARE';
    }
    else {
        $s = "Repair: processed $nid, case=$case";
        __output("$s\n\n");
        r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

        return $case;
    }
}

# ---------------------------------------------------------------------

=item r_rights_Log

Description

=cut

# ---------------------------------------------------------------------
my $LOG_BUFFER = '';
my $Log_buffer_size = 0;
use constant LOG_BUFFER_MAX => 1000;

sub r_rights_Log {
    my ($C, $s, $key) = @_;

    return if ($CHECK);

    my $ss = qq{$s at: } . Utils::Time::iso_Time() . qq{\n};
    $LOG_BUFFER .= $ss;
    $Log_buffer_size++;
    if ($Log_buffer_size > LOG_BUFFER_MAX) {
        flush_r_rights_Log($C, $key);
    }
}

sub flush_r_rights_Log {
    my ($C, $key) = @_;

    return if ($CHECK);

    SLIP_Utils::Log::this_string($C, $LOG_BUFFER, $key, '___RUN___', 'rights', 'no_newline');
    $LOG_BUFFER = '';
    $Log_buffer_size = 0;
}


# ---------------------------------------------------------------------

=item __hiresTime

Description

=cut

# ---------------------------------------------------------------------
sub __hiresTime
{
    my ($seconds, $microseconds) = Time::HiRes::gettimeofday();
    my $fraction = sprintf("%.8f", $microseconds/1000000);
    return sprintf("%.8f", $seconds + $fraction);
}

# ---------------------------------------------------------------------

=item initialize_j_rights_temp

Description

=cut

# ---------------------------------------------------------------------
sub initialize_j_rights_temp {
    my ($C, $dbh) = @_;
    Db::initialize_j_rights_temp($C, $dbh);
}

# ---------------------------------------------------------------------

=item DROP_j_rights_RENAME_j_rights_temp

Description

=cut

# ---------------------------------------------------------------------
sub DROP_j_rights_RENAME_j_rights_temp {
    my ($C, $dbh) = @_;
    Db::Drop_j_rights_Rename_j_rights_temp($C, $dbh);

    my $s = "Rebuild: drop j_rights, rename j_rights_temp";
    __output("$s\n\n");
    r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

}

# ---------------------------------------------------------------------

=item process_rights_from_errors

Check errors-j for metadata errors that may have been due to the id
MISSING FROM rights-j [because of various types of errors] and, if we
can get the metadata, add that id to rights-j.

This at least allows Collection Builder indexing for these missing ids
short of totally rebuilding rights-j table.

=cut

# ---------------------------------------------------------------------
sub process_rights_from_errors {
    my ($C, $dbh, $run, $Check, $totals_hashref) = @_;

    while (Utils::GlobalSwitch::cron_jobs_disabled('slip', 'STOPSLIP')) {
        __output("  wait Rights done: STOPSLIP\n");
        sleep STOPSLIP_SLEEP_WAIT;
    }

    my $s;

    $totals_hashref->{num_m_errors} = 0;
    $totals_hashref->{num_m_errors_repaired} = 0;
    $totals_hashref->{num_m_not_missing} = 0;
    $totals_hashref->{num_m_anomalies} = 0;
    $totals_hashref->{num_m_vufind_http} = 0;
    $totals_hashref->{num_m_missing_vufind} = 0;
    $totals_hashref->{num_m_missing_rights} = 0;

    my $id_arr_hashref = Db::Select_error_ids($C, $dbh, $run, undef, IX_METADATA_FAILURE);
    my $num_metadata_errors = scalar(@$id_arr_hashref);
    if ($num_metadata_errors) {
        $s = qq{Repair: Trying to repair $num_metadata_errors ids from j_errors };
        __output(qq{$s} . Utils::Time::iso_Time() . qq{\n});
        r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

        $totals_hashref->{num_m_errors} = $num_metadata_errors;

        my $searcher = SLIP_Utils::Solr::create_VuFind_Solr_Searcher_by_alias($C);
        my $rs = new Search::Result::vSolrRaw();

        foreach my $hashref (@$id_arr_hashref) {
            my $id = $hashref->{'id'};

            my $case = repair_insert_id($C, $dbh, $id, $Check, $searcher, $rs);
            if ($case eq 'NEW') {
                # Repair succeeded: ID added to j_rights
                $totals_hashref->{num_m_errors_repaired}++;
            }
            elsif ($case eq 'NOOP') {
                $totals_hashref->{num_m_not_missing}++;
            }
            elsif (grep(/^$case$/, ('MOVED', 'UPDATED'))) {
                $totals_hashref->{num_m_anomalies}++;
            }
            elsif ($case eq 'VuFind_BADHTTP') {
                $totals_hashref->{num_m_vufind_http}++;
            }
            elsif ($case eq 'VuFind_MISSING') {
                $totals_hashref->{num_m_missing_vufind}++;
            }
            elsif ($case eq 'Rights_FAIL') {
                $totals_hashref->{num_m_missing_rights}++;
            }
        }
    }
    else {
        $s = qq{Repair: No j_errors ids to repair };
        __output(qq{$s} . Utils::Time::iso_Time() . qq{\n});
        r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);
    }
}

# ---------------------------------------------------------------------

=item process_rights

To build, get the timestamp recorded when VuFind Solr was last queried
for IDs.  This timestamp will not be updated until we successfully
process the entire Solr range query :[$timestamp -2d TO *].  If FAIL,
we must redo:

% rights-j -B | -R

Query for a day in the past with a +ve $INCREMENT to prevent id loss
updates to the vSolr index in the *same* day *after* rights-j runs on
that day.  NOTE: This is not expected to happen. Negative values
advance the timestamp.

Insert in blocks of SLICE_SIZE. Anomalies count items from bib
records that are not in mdp.rights.  These are logged and must be
corrected manually.

=cut

# ---------------------------------------------------------------------
sub process_rights {
    my ($C, $dbh, $run, $timestamp, $Check, $Rebuild) = @_;

    my $process_start = __hiresTime();

    my $LAG = $C->get_object('MdpConfig')->get('vSolr_num_lag_days');
    my $query_timestamp = SLIP_Utils::Common::get_offset_ISO_timestamp($timestamp, $LAG);

    my $s0 = qq{[BEGIN]: $INIT_MSG CHECK=$CHECK };
    __output(qq{$s0} . Utils::Time::iso_Time() . qq{\n});
    r_rights_Log($C, $s0, $RIGHTS_LOGFILE_KEY);

    my %totals =
        (
         'bib_record_ct' => 0,
         'ids' => 0,
         'anomalies' => 0,
         'NEW' => 0,
         'NOOP' => 0,
         'MOVED' => 0,
         'UPDATED' => 0,
         'meta_upd' => 0,
        );

    goto REPAIR if (DEBUG('repair'));

    my $offset = 0;
    my $slice = 0;
    while (1) {
        while (Utils::GlobalSwitch::cron_jobs_disabled('slip', 'STOPSLIP')) {
            __output("  wait Rights done: STOPSLIP\n");
            sleep STOPSLIP_SLEEP_WAIT;
        }

        my %slice =
        (
         'NEW' => 0,
         'NOOP' => 0,
         'MOVED' => 0,
         'UPDATED' => 0,
        );

        my $slice_start = __hiresTime();

        # Get nids from vSolr bib records that are newer than the last
        # time we looked (or all of them if REBUILDing).
        my ($ref_to_arr_of_hashref, $bib_record_ct, $anomalies) =
            vSolr::get_item_updated_nid_slice_as_of($C, $dbh, $query_timestamp, $offset, $SLICE_SIZE, $Rebuild);

        $slice++;
        $totals{'bib_record_ct'} += $bib_record_ct;
        $totals{'anomalies'} += $anomalies;

        my $ids = scalar(@$ref_to_arr_of_hashref);
        if ($ids <= 0) {
            my $s1 = qq{Slice: num=$slice offset=$offset ids_in_slice=$ids vq_time=$query_timestamp };
            __output(qq{$s1} . Utils::Time::iso_Time() . qq{\n});
            r_rights_Log($C, $s1, $RIGHTS_LOGFILE_KEY);

            last;
        }

        $totals{'ids'} += $ids;

        my $s2 = qq{[SLICE $slice] offset=$offset ids_in_slice=$ids vq_time=$query_timestamp };
        __output(qq{$s2} . Utils::Time::iso_Time() . qq{\n});
        r_rights_Log($C, $s2, $RIGHTS_LOGFILE_KEY);

        foreach my $hashref (@$ref_to_arr_of_hashref) {
            my $case = Db::Replace_j_rights_id($C, $dbh, $hashref, $Check, $Rebuild);

            if (! grep(/^$case$/, ('NEW', 'NOOP', 'MOVED', 'UPDATED'))) {
                my $s3 = qq{Software error: invalid case value=$case } . Utils::Time::iso_Time();
                r_rights_Log($C, $s3, , $RIGHTS_LOGFILE_KEY);
                die $s3;
            }

            $slice{$case}++;
            $totals{$case}++;
            r_event_report($C, $dbh, $offset, $hashref, $case);

            # Add updated and moved ids to the Collection Builder metadata update queue
            my $skip_metadata_queue = ($Check || $Rebuild);
            if (! $skip_metadata_queue) {
                if ( grep(/^$case$/, ('MOVED', 'UPDATED')) ) {
                    queue_id_for_metadata_update($C, $dbh, $hashref);
                    $totals{meta_upd}++;
                }
            }
        }
        
        r_slice_report($C, $dbh, $slice_start, $offset, $ids, $anomalies, \%slice, \%totals);

        $offset += $SLICE_SIZE;
    }

    # Update the vSolr timestamp to the MAX(update_time) in
    # j_rights{_temp}. We will backtrack by 2 days from this value
    # when next we run.
    Db::update_vSolr_timestamp($C, $dbh, $Rebuild)
        unless $Check;

  REPAIR:
    process_rights_from_errors($C, $dbh, $run, $Check, \%totals)
      unless $Rebuild;

    return ($process_start, \%totals);
}

# ---------------------------------------------------------------------

=item queue_id_for_metadata_update

Description

=cut

# ---------------------------------------------------------------------
sub queue_id_for_metadata_update {
    my ($C, $dbh, $hashref) = @_;
    
    my $id = $hashref->{'nid'};
    my $ok = MetadataQueue::enqueue_metadata_item_id($C, $dbh, $id);

    return $ok;
}


# ---------------------------------------------------------------------

=item near_vSolr_downtime

Description

=cut

# ---------------------------------------------------------------------
sub near_vSolr_downtime {
    my $C = shift;
    my $config = $C->get_object('MdpConfig');

    my $near = 0;

    return
        if (! $config->get('vSolr_downtime_checking'));

    my @downtime = $config->get('vSolr_downtime_interval');

    my ($second, $minute, $hour) = localtime();
    my $decimal_hour = $hour + $minute/60.0;

    if (
        ($decimal_hour > $downtime[0])
        &&
        ($decimal_hour < $downtime[1])
       ) {
        $near = 1;
    }

    return $near;
}


# ---------------------------------------------------------------------

=item r_rights_enabled

Is j_rights enabled?

=cut

# ---------------------------------------------------------------------
sub r_rights_enabled {
    my ($C, $dbh) = @_;

    my $enabled = Db::Select_rights_enabled($C, $dbh);
    return $enabled;
}

# ---------------------------------------------------------------------

=item r_slice_report

Description

=cut

# ---------------------------------------------------------------------
sub r_slice_report {
    my ($C, $dbh, $slice_start, $offset, $slice_ids, $anomalies, $sref, $tref) = @_;

    my $slice_elapsed = __hiresTime() - $slice_start;
    my $idsPsec = sprintf("%.2f", ($slice_ids + $anomalies)/$slice_elapsed);

    my $size = Db::Select_count_from_j_rights($C, $dbh);
    my $temp_size = 0;
    if ($REBUILD) {
        $temp_size = Db::Select_count_from_j_rights_temp($C, $dbh);
    }

    my $s =
        qq{Checkpoint: j_rights size=$size j_rights_temp size=$temp_size off=$offset\n}
            . qq{  slice_ids=$slice_ids anom=$anomalies new=$sref->{'NEW'} noop=$sref->{'NOOP'} upd=$sref->{'UPDATED'} mov=$sref->{'MOVED'}\n}
                . qq{  total_ids=$tref->{'ids'} total_anom=$anomalies total_new=$tref->{'NEW'} total_noop=$tref->{'NOOP'} total_upd=$tref->{'UPDATED'} total_mov=$tref->{'MOVED'} total_meta_updates=$tref->{'meta_upd'}\n}
                    . qq{  rate=$idsPsec ids/sec };

    __output(qq{$s} . Utils::Time::iso_Time() . qq{\n});
    r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);
}

# ---------------------------------------------------------------------

=item r_downtime_report

Description

=cut

# ---------------------------------------------------------------------
sub r_downtime_report {
    my ($C, $timestamp) = @_;

    my $s = qq{DOWNTIME: timestamp=$timestamp };
    __output(qq{$s} . Utils::Time::iso_Time() . qq{\n});
    r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);
}

# ---------------------------------------------------------------------

=item r_event_report

Description

=cut

# ---------------------------------------------------------------------
sub r_event_report {
    my ($C, $dbh, $offset, $hashref, $event) = @_;

    my $nid = $hashref->{'nid'};
    my $attr = $hashref->{'attr'};
    my $reason = $hashref->{'reason'};
    my $source = $hashref->{'source'};
    my $user = $hashref->{'user'};
    my $time = $hashref->{'time'};
    my $sysid = $hashref->{'sysid'};

    # Get supplements to the mdp.rights data queried from j_rights and nid processing
    my $timestamp_of_nid = $hashref->{'timestamp_of_nid'};
    my $timestamp_in_j_rights = $hashref->{'timestamp_in_j_rights'};
    my $sysid_in_j_rights = $hashref->{'sysid_in_j_rights'};

    my $s =
        qq{$event: nid=$nid attr=$attr reason=$reason source=$source user=$user time=$time timestamp_of_nid=$timestamp_of_nid timestamp_in_j_rights=$timestamp_in_j_rights j_rights_sysid=$sysid_in_j_rights vsolr_sysid=$sysid };
    DEBUG('vsolr', qq{DEBUG: $s} . Utils::Time::iso_Time() . qq{\n});
    r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);
}

# ---------------------------------------------------------------------

=item __get_repair_report

Description

=cut

# ---------------------------------------------------------------------
sub __get_repair_report {
    my $tr = shift;
    my $s =
      qq{Repair metadata errors from j_rights:}
        . qq{\n\tj_errors_candidates=$tr->{num_m_errors} }
          . qq{\n\tj_rights_repaired=$tr->{num_m_errors_repaired} }
            . qq{\n\tj_rights_not_missing=$tr->{num_m_not_missing} }
              . qq{\n\tanomalies=$tr->{num_m_anomalies} }
                . qq{\n\tvufind_http_error=$tr->{num_m_vufind_http} }
                  . qq{\n\tvufind_not_present=$tr->{num_m_missing_vufind} } 
                    . qq{\n\trights_not_present=$tr->{num_m_missing_rights} };
    return $s;
}

# ---------------------------------------------------------------------

=item r_final_report

Description

=cut

# ---------------------------------------------------------------------
sub r_final_report {
    my ($C, $dbh, $process_start, $tref, $Check, $Rebuild) = @_;

    my $process_elapsed = __hiresTime() - $process_start;
    my $process_elapsed_min = sprintf("%.2f", $process_elapsed/60);
    my $idsPsec = sprintf("%.2f", ($tref->{'ids'} + $tref->{'anomalies'})/$process_elapsed);

    my $size = Db::Select_count_from_j_rights($C, $dbh);
    my $temp_size = 0;
    if ($Rebuild) {
        $temp_size = Db::Select_count_from_j_rights_temp($C, $dbh);
    }

    my $timestamp = Db::Select_vSolr_timestamp($C, $dbh);

    my $run_total = $tref->{'NEW'} + $tref->{'UPDATED'} + $tref->{'MOVED'};
    my $repair_report = __get_repair_report($tref)
	unless($Rebuild);

    my $r = $Rebuild ? '(before j_rights_temp rename)' : '';
    my $s =
        qq{\nUpdate$r: j_rights size=$size j_rights_temp size=$temp_size total_ids_seen=$tref->{'ids'} total_bib_seen=$tref->{'bib_record_ct'}\n}
            . qq{\tthis_run_total=$run_total :: total_new=$tref->{'NEW'} total_upd=$tref->{'UPDATED'} total_mov=$tref->{'MOVED'}\n}
                . qq{\ttotal_noop=$tref->{'NOOP'} total_anom=$tref->{'anomalies'} total_meta_updates=$tref->{'meta_upd'}\n}
                    . qq{\trate=$idsPsec id/sec t=$timestamp elapsed=$process_elapsed_min min\n}
                      . qq{$repair_report};

    __output(qq{$s} . Utils::Time::iso_Time() . qq{\n});
    r_rights_Log($C, $s, $RIGHTS_LOGFILE_KEY);

    if (! $INTERACTIVE || $FORCE_EMAIL) {
        my $HOST = `hostname`;
        $HOST =~ s,\..*$,,s;
        my $subj = qq{[SLIP] j-rights report ($HOST)};
        SLIP_Utils::Common::Send_email($C, 'report', $subj, $s);
    }
}


1;


=head1 AUTHOR

Phillip Farber, University of Michigan, pfarber@umich.edu

=head1 COPYRIGHT

Copyright 2009-2010 Â©, The Regents of The University of Michigan, All Rights Reserved

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and associated documentation files (the
"Software"), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject
to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

=cut
