#!/usr/bin/env perl

=head1 NAME

manage-item-index-j

=head1 USAGE

see usage

=head1 DESCRIPTION

Stop tomcat, mv index, restart tomcat, delete index.

Production:

The ptsearch solr/tomcat instance runs on slurm-1 at the MACC and duff-1 at ICTC.

Solr connection information for apps running on the web servers is: http://solr-sdr-ptsearch:8080/ptsearch

Solr home at each site is /l/solrs/prod/ptsearch.

User must have sudo rights to start/stop the tomcat instance on slurm-1/duff-1 (/etc/init.d/tomcat-ptsearch).

Logs are in /l/local/apache-tomcat-ptsearch/logs.

=head1 OPTIONS

=over 8

=item -

see help

=back

=cut

use strict;
BEGIN {
    #$ENV{DEBUG_LOCAL} = 1;
}

# ----------------------------------------------------------------------
# Set up paths for local libraries -- must come first
# ----------------------------------------------------------------------
use lib "$ENV{SDRROOT}/mdp-lib/Utils";
use Vendors;


# Perl
use Getopt::Std;

# App
use Utils;
use Utils::Time;
use Utils::GlobalSwitch;
use Debug::DUtils;

use Context;
use MdpConfig;
use Search::Constants;
use Semaphore;

# Local
use Db;
use SLIP_Utils::Common;
use SLIP_Utils::States;
use SLIP_Utils::Log;
use SLIP_Utils::DatabaseWrapper;
use Search::Result::SLIP_Raw;
use SLIP_Utils::Solr;

# Flush i/o
$| = 1;

sub mii_get_usage {
    my $s .= qq{Usage: manage-item-index-j -r run [-d][-n][-f]
       where -n runs without calling start, stop or move/delete.
             -f forces email in interactive mode\n};
    return $s;
}

our ($opt_r, $opt_d, $opt_n, $opt_T, $opt_f);

my $ops = getopts('r:dnTf');

use constant TOMCAT_MAX_TRIES     => 10;
use constant TOMCAT_TRIES_WAIT    => 30; # 0.5 minute

my $HOST = `hostname`; $HOST =~ s,\..*$,,s;

my @SOLR_HOSTS = ('slurm-1', 'duff-1', 'alamo');

my $REPORT_BUFFER = '';
my $BASE_DIR = $ENV{'SDRROOT'} . '/slip/index/';
my $INTERACTIVE = $ENV{'TERM'};

my $FORCE_EMAIL = defined($opt_f);
my $NOOP = defined($opt_n); # optional

my $RUN = $opt_r; # Required
if (! $RUN) {
    my $s = qq{run (-r) parameter missing } . mii_get_usage();
    my $rc = $SLIP_Utils::States::RC_BAD_ARGS;
    __output($s);
    __non_interactive_err_output($rc, $s);

    exit $rc;
}

my $TEST_ONLY = defined($opt_T);
exit 0 if ($TEST_ONLY);

if (! $RUN) {
    my $s = qq{run (-r) parameter missing } . mii_get_usage();
    my $rc = $SLIP_Utils::States::RC_BAD_ARGS;
    __output($s);
    __non_interactive_err_output($rc, $s);

    exit $rc;
}

if (defined($opt_d)) {
    $ENV{'DEBUG'} .= 'lsdb,me';
}

my $C = new Context;

my $config = SLIP_Utils::Common::gen_SLIP_config($RUN);
$C->set_object('MdpConfig', $config);

# constrain to proper site
my $hosting_site = $config->get('hosting_site');        
my $site = Search::Site::get_server_site_name();
if ($site ne $hosting_site) {
    my $s = qq{stats-item-index-j run=$RUN wrong site: $site is not $hosting_site};
    Log_manage_error($C, $RUN, $s);
    __output("$s\n");
    my $rc = $SLIP_Utils::States::RC_BAD_ARGS;
    __non_interactive_err_output($rc, $s);
    exit $rc;
}

my $DBH = SLIP_Utils::DatabaseWrapper::GetDatabaseConnection($C, qq{manage-item-index-j run=$RUN host=$HOST});


# Do not try to manage while I am running already
my $Lock_File = "/tmp/manage-item-index-lock.sem";
my $Semaphore = new Semaphore($Lock_File);
if (! $Semaphore) {
    my $s = "Could not get semaphore file: $Lock_File at host=$HOST";
    __output("$s\n");
    Log_manage_error($C, $RUN, $s);
    exit 0;
}
END {
    $Semaphore->unlock()
        if ($Semaphore);
}

my $SEARCHER = SLIP_Utils::Solr::create_shard_Searcher_by_alias($C, 1);
my $engine_uri = $SEARCHER->get_engine_uri();

if ($INTERACTIVE) {
    if (! grep(/^$HOST$/, @SOLR_HOSTS)) {
        if (! $NOOP) {
            my $s = qq{$HOST not in solr host list (} . join(', ', @SOLR_HOSTS) . qq{) for restart.\nExit.\n};
            __output($s);
            exit 0;
        }
    }
}

eval {
    my $s = "manage-item-index-j ($HOST $engine_uri): MANAGE" . ($NOOP ? " ---> NOOP" : "") ;
    __output("$s\n");
    Log_manage($C, $RUN, $s);
    
    handle_mii_rc($C, $DBH, $RUN, $HOST,
                  mii_manage($C, $RUN, $DBH));
};
if ($@) {
    Log_manage_error($C, $RUN, "Critical error: $@");
    handle_mii_rc($C, $DBH, $RUN, $HOST, $SLIP_Utils::States::RC_CRITICAL_ERROR);
    # NOTREACHED
}


exit 0;

#
# --------------------- S u b r o t i n e s   -------------------------
#

# ---------------------------------------------------------------------

=item handle_mii_rc

Description

=cut

# ---------------------------------------------------------------------
sub handle_mii_rc {
    my ($C, $dbh, $run, $host, $rc) = @_;

    # Log
    my $s = SLIP_Utils::Common::stage_rc_to_string($rc) . qq{ run=$run host=$host};
    __output(qq{$s\n});
    __non_interactive_err_output($rc, $s);

    if ($rc > 0) {
        Log_manage_error($C, $run, $s);
    }
    else {
        Log_manage($C, $run, $s);
    }

    # Report stats
    mii_email_report($C, $run);

    exit $rc if ($rc > 0);
}

# ---------------------------------------------------------------------

=item mii_email_report

Description

=cut

# ---------------------------------------------------------------------
sub mii_email_report {
    my ($C, $run) = @_;

    return unless($FORCE_EMAIL || (! $INTERACTIVE));

    my $subj = qq{[SLIP] manage-item-index-j ($HOST) -r$run};
    SLIP_Utils::Common::Send_email($C, 'report', $subj, $REPORT_BUFFER);
}


# ---------------------------------------------------------------------

=item mii_run_s_tomcat

Description

=cut

# ---------------------------------------------------------------------
sub mii_run_s_tomcat {
    my ($C, $run, $action) = @_;

    return 0 if $NOOP;

    my $rc = 0;
    my $fail_rc = ($action eq 'stop')
      ? $SLIP_Utils::States::RC_TOMCAT_STOP_FAIL
        : $SLIP_Utils::States::RC_TOMCAT_START_FAIL;

    my $test = ($action eq 'stop') ? 0 : 1;

    # Try to act on a tomcat
    my $cmd = qq{sudo  /etc/init.d/tomcat-ptsearch $action};
    my $output = `$cmd 2>&1`;
    my $sysrc = $? >> 8;
    
    chomp($output);
    
    
    # The rc from tomcat-build is unreliable. Test result by looking at
    # process table to make sure sure it stopped or started
    my $tomcat_pattern = $C->get_object('MdpConfig')->get('tomcat_pattern');

    my $tries = 0;
    while (1) {
        last if (SLIP_Utils::Processes::is_tomcat_running($C, $tomcat_pattern) == $test);
        sleep TOMCAT_TRIES_WAIT;
        $tries++;
        last if ($tries > TOMCAT_MAX_TRIES);
    }

    my $total_wait_time = $tries * TOMCAT_TRIES_WAIT;
        
    if ($tries > TOMCAT_MAX_TRIES) {
        $rc = $fail_rc;
        my $s = qq{manage-item-index-j tomcat failed action=$action, wait time=$total_wait_time sec. output="$output" sysrc=$sysrc};
        __output("$s\n");
        __non_interactive_err_output($rc, $s);
        Log_manage_error($C, $run, $s);
    }
    else {
        my $s = qq{manage-item-index-j success: tomcat action=$action wait time=$total_wait_time sec. output="$output" sysrc=$sysrc};
        __output("$s\n");
        Log_manage($C, $run, "mii_run_s_tomcat (action=$action): " . $output);
    }

    return $rc;
}

# ---------------------------------------------------------------------

=item mii_do_move_index

Minimize the stop, delete, start cycle by just mv the index.  Later delete it.

=cut

# ---------------------------------------------------------------------
sub mii_do_move_index {
    my ($C, $dbh, $run) = @_;

    return 0 if $NOOP;

    # Just mv the index dir
    my $key = 'dir_for_shard_1';
    my $index_dir = $C->get_object('MdpConfig')->get($key);
    my $mv_to_index_dir = "$index_dir" . "-DELETEME-$$";
    
    my $rc = system("mv", "$index_dir", $mv_to_index_dir);

    if ($rc > 0) {
        my $s = qq{manage-item-index-j failed to move $index_dir to $mv_to_index_dir rc=$rc, $!};
        __output("$s\n");
        Log_manage_error($C, $run, $s);
    }
    else {
        my $s = qq{manage-item-index-j moved $index_dir to $mv_to_index_dir success};
        __output("$s\n");
        Log_manage($C, $run, $s);
    }

    # Regardless, let Tomcat try to restart (rc=0)
    return 0;
}

# ---------------------------------------------------------------------

=item mii_do_delete_index

Delete the moved index at our leisure.

=cut

# ---------------------------------------------------------------------
sub mii_do_delete_index {
    my ($C, $dbh, $run) = @_;

    return 0 if $NOOP;

    # Remove files- if failure just report it and allow tomcat to try
    # to restart on this index.
    my $key = 'dir_for_shard_1';
    my $index_dir = $C->get_object('MdpConfig')->get($key);
    my $index_dir_parent = "$index_dir/..";
    
    if (! opendir(DIR, $index_dir_parent)) {
        my $s = qq{manage-item-index-j $HOST failed to open $index_dir_parent: $!};
        __output("$s\n");
        Log_manage_error($C, $run, $s);
        return 0;
    }

    my @dir_list = map { "$index_dir_parent/$_" } grep(! /^(\.|\..)/, readdir(DIR));
    closedir(DIR);

    my $DELS_present = 0;
    foreach my $dir (@dir_list) {
        if ($dir =~ m,DELETEME,) {
            $DELS_present = 1;
            my $rc = system("rm", "-r", "-f", "$dir");

            if ($rc > 0) {
                my $s = qq{manage-item-index-j $HOST failed to delete $dir: sysrc=$rc, $!};
                __output("$s\n");
                Log_manage_error($C, $run, $s);
            }
            else {
                my $s = qq{manage-item-index-j $HOST deleted $dir success};
                __output("$s\n");
                Log_manage($C, $run, $s);
            }
        }
    }

    if (! $DELS_present) {
        my $s = qq{manage-item-index-j $HOST 0 DELETEME directories present at $index_dir_parent};
        __output("$s\n");
        Log_manage($C, $run, $s);
    }

    return 0;
}


# ---------------------------------------------------------------------

=item mii_manage

Stop, delete, start

=cut

# ---------------------------------------------------------------------
sub mii_manage {
    my ($C, $run, $dbh) = @_;

    # Try to stop a tomcat to free up memory
    my $rc = mii_run_s_tomcat($C, $run, 'stop');
    return $rc if ($rc > 0);

    # Move index
    $rc = mii_do_move_index($C, $dbh, $run);
    return $rc if ($rc > 0);

    # Try to re-start a tomcat
    $rc = mii_run_s_tomcat($C, $run, 'start');
    return $rc if ($rc > 0);

    # Delete index
    $rc = mii_do_delete_index($C, $dbh, $run);
    return $rc;
}

# ---------------------------------------------------------------------

=item Log_manage

Description

=cut

# ---------------------------------------------------------------------
sub Log_manage {
    my ($C, $run, $s) = @_;

    my $s0 = qq{***MANAGE: } . Utils::Time::iso_Time() . qq{ r=$run, $HOST, $s};
    $REPORT_BUFFER .= $s0 . "\n";
    SLIP_Utils::Log::this_string($C, $s0, 'manage_logfile', '___RUN___', $run);
}


# ---------------------------------------------------------------------

=item Log_manage_error

Description

=cut

# ---------------------------------------------------------------------
sub Log_manage_error {
    my ($C, $run, $error) = @_;

    my $s = qq{***MANAGE [ERROR]: } . Utils::Time::iso_Time() . qq{ r=$run, $HOST, $error};
    $REPORT_BUFFER .= $s . "\n";

    SLIP_Utils::Log::this_string($C, $s, 'manage_logfile', '___RUN___', $run);
}

1;


=head1 AUTHOR

Phillip Farber, University of Michigan, pfarber@umich.edu

=head1 COPYRIGHT

Copyright 2011 Â©, The Regents of The University of Michigan, All Rights Reserved

Permission is hereby granted, free of charge, to any person obtaining
a copy of this software and associated documentation files (the
"Software"), to deal in the Software without restriction, including
without limitation the rights to use, copy, modify, merge, publish,
distribute, sublicense, and/or sell copies of the Software, and to
permit persons to whom the Software is furnished to do so, subject
to the following conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND,
EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF
MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT.
IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY
CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT,
TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE
SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.

=cut



